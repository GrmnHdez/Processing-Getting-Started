### DIVULGACIÓN.

__Processing__ es un programa que permite hacer imágenes interactivas, visualización de datos y gráficos generativos en tiempo real. 


Las imagenes como pinturas en cuadros,historietas o las fotos de las revistas, representan momentos estaticos o memorias de quien realiza la imagen. __Processing__ es un software que permite transformar imagenes quietas en graficos interactivos, ya que estos pueden reaccionar a diversos factores externos, como por ejemplo al pulsar una tecla del computador, mover el mouse o un sonido._Delicate BounDaries_ de __chris sugrue__ [link](https://vimeo.com/1007230), es una instalación interactiva donde en una pantalla se proyecta un ecosistema donde viven bichos, que cuando la persona toca esa pantalla, los bichos salen de su habitat para incorporarse en el cuerpo del usuario.

__Procesing__ támbien permite que los computadores puedan crear imagenes por medio de formulas matematicas,  este proceso puede generar imagenes que son alucivas a la naturaleza como por ejemplo Arboles, plantas y flores. __György Kepes__ es un artista que usa los procesos del arte generativo para crear imagenes que evolucionan en tiempo real. [link](http://butdoesitfloat.com/The-line-that-describes-the-beautiful-is-elliptical-It-has-simplicity)

Finalmente, __Processing__ es una gran herramienta para procesar y visualizar datos en tiempo real. Los datos son cualquier información que se pueda convertir en numeros y que varie con el tiempo. Por ejemplo las unidades de medida, el clima o los votos en tiempo de elecciones. En la música, las notas musicales tambien se pueden representar en numeros por medio de un protocolo llamado MIDI. Este es usado para comunicar diversos aparatos musicales entre si. El MIDI támbien es usado para crear el componente visual de conciertos en vivo, creando shows visualmente asombrosos, por su sincronización entre música y visuales en tiempo real.
Tenemos como referente a __Etienne de Crécy__ y __1024__ que a mi parecer son los mas innovadores en este asunto de las sincronización de imagen en tiepo real. [link](https://www.youtube.com/watch?v=vXE2x3y51Fk)



## Descripción y practicas.

Cuando dibujamos cualquier figura en una hoja de papel, podemos hacer pequeños trazos que siempre terminan en figuras, que el diseño gráfico llama bocetos. Pero cuando tomamos esa imagen, y la capturamos en una fotografía, tenemos una representación digital de ese boceto. Por ejemplo: las maquinas de escribir, que en décadas atrás se usaban para redactar documentos y realizar cartas; fue reemplazada por un tipo de tecnología digital como el computador, el cual posee un software llamado Word, que permite reemplazar la maquina de escribir.

__Processing__ es un programa de computadora que permite hacer bocetos de manera escrita. Pero antes de que el programa pueda entender que queremos hacer, nosotros necesitamos aprender un lenguaje que la maquina puede descifrar, en pocas palabras necesitamos comunicarnos con la maquina. De este modo,  así mismo cuando queremos comunicarnos con un extranjero y debemos hablar en su idioma para poder entablar una conversación, el software __Processing__ entiende un lenguaje destilado de Java, que es un lenguaje para escribir software de computadoras.

Ahora, ¿ Cómo hace el computador para entender todo ese texto que escribimos si el no sabe nuestro idioma ?. Antes de responder esa pregunta, tenemos que enterarnos  que una computadora es solamente una maquina como cualquier otra, que sin una orden, es incapaz de realizar cualquier acción.  De esta forma se establece una comunicación usuario/maquina y se fijan ordenes mas especificas al computador.

¿ Te parece complicado ? la verdad es que no lo es; __Processing__ se destaca porque tiene una sintaxis (esas oraciones) fácil de aprender, ya que con solo una oración (línea de código) podemos ver un circulo en la pantalla;  si escribimos unas cuantas líneas  de código más,  ese circulo puede seguir la posición del ratón. 

 Adentrémonos un poco en lo que para mi es el fascinante mundo de la programación. Imagínese describiendo todos los pasos necesarios para enseñarle a un robot a vestirse. Muchas personas resolverían el problema con decirle al robot:

1.	busca la ropa.
2.	busca una camisa y un pantalón.
3.	ponte la camisa.
4.	ponte el pantalón.

Pero al intentar ejecutar la orden número uno, el robot colapse porque no sabe donde esta la ropa, entonces quizá debemos ser más específicos.
Otra solución mas detallada seria:

1.	dirígete al closet.
2.	Abre el closet.
3.	Mira hacia la derecha
4.	Levanta la mano derecha
5.	Elige una camisa y tómala.
6.	Mira hacia la izquierda.
7.	Toma un pantalón.
8.	Deja las prendas tiradas en el suelo.
9.	Cierra el closet
10.	Recoge las prendas.
11.	Ponte la camisa
12.	Ponte el pantalón.

Hay ocasiones en que, incluso, debemos ser mas explícitos y  descriptivos. Programar una computadora es, sencillamente, describir los pasos necesarios para resolver un problema. Por consiguiente, para dibujar animaciones,  podríamos usar pasos similares a los que se suelen realizar al hacer, por ejemplo, cálculos complejos, análisis estadísticos. 

Actualmente, las practicas que unen arte y tecnología se hibridan de tal manera que esas barreras decaen, conllevando a que una persona; pueda ser parte activa de manera consiente o inconsciente, con una obra de origen tecnológico realizada con código. Hablamos entonces de arte Interactivo o New Media Art: el cual se lleva a cabo procesos artísticos y herramientas tecnológicas. Este es el punto fuerte de __Processing__, ya que al tratarse de un lenguaje de programación, todos nuestros dibujos pueden ser interactivos: esto quiere decir que esos gráficos en pantalla pueden reaccionar al teclado, al presionar el ratón y con ayuda de cámaras; podemos hacer seguimiento de colores o reconocimiento de rostros, si se es un entusiasta y se conoce lo mas mínimo de electrónica, podemos añadir a nuestros dibujos otras formas de control como sensores e interfaces personalizadas. Esa es la gran diferencia de dibujar con un papel y usar una computadora. __Processing__ puede hacer que simples esbozos se conviertan en “dibujos inteligentes”, ya que esos gráficos pueden tomar decisiones o quizás puedan reaccionar a algo si así lo queremos. En definitiva, cuando estamos dibujando por medio de código podemos hacer la semejanza de que estamos creando un universo por medio de un lenguaje escrito.

Actualmente, en Medellín se están generando nuevos campos de acción encaminados a tejer esfuerzos que conjugan Arte y tecnología. Es por ello que __Processing__ apunta a crear nuevos diseñadores con un alto grado de énfasis en aplicaciones interactivas, usando lenguaje de computadoras para crear sistemas inteligentes, causando que cualquier tipo de tecnología móvil, digital o análoga pueda incluirse como herramienta para cualquier proyecto creativo. Es en este punto donde se esta conjugando el arte y tecnología para que Medellín se destaque como territorio innovador de artistas a nivel nacional.

Para empezar con __Processing__ debemos dirigirnos a su portal [web]( https://processing.org/download/), y descargarlo porque es libre como el aire; nos preguntaran si queremos hacer un tipo de donación para financiar el proyecto, luego lo instalamos en nuestra computadora y estamos listo para iniciar.

La interfaz de __Processing__ esta compuesta por una serie de botones en la parte superior los cuales pueden ser intuitivos, un botón de start, otro de stop y otros que explicaremos mas adelante. 

Debajo, podemos mirar una gran zona blanca; llamada también editor de texto. Esta es la zona mas importante, ya que aquí será donde escribiremos nuestras oraciones o líneas de código.  

Empecemos por escribir esto:

```
size(250, 250);
background(0);
fill(255, 0, 0);
ellipse(width/2, height/2, 60, 60);
```

Después de escribir o copiar este pequeño código en __processing__, presionamos start para que el programa traduzca esas oraciones en una imagen. Podemos ver un circulo en la mitad de la pantalla, un fondo negro y el circulo pintado de rojo.

Expliquemos mas detalladamente el programa anterior.

Empezamos por decir “size( 250, 250 );”size es una función que se usa para inicializar el tamaño de nuestra hoja de dibujo, en este caso tenemos una hoja de 250 pixeles de ancho por 250 pixeles de alto. Seguidamente, tenemos “background( 0 );”, esta función se usa para dar un color de fondo a nuestra pantalla, que en este caso es negro. Después tenemos otra función que dicta “fill( 255, 0, 0);” la cual se usa para pintar el relleno de las figuras, que en este caso se usa para pintar un circulo de color rojo. Y finalmente dibujamos nuestro circulo, tenemos “ellipse( width/2, height/2, 60, 60);” con esa oración el programa entiende: dibújame un circulo en la posición del eje X en el centro de la pantalla y en la posición Y den el dentro de la pantalla, además; quiero que ese circulo tenga 60 pixeles de ancho y 60 pixeles de alto.

Te invito a que procedas a cambiar el código, añadiendo otros números en fill(); o background(); . Experimentar es una muy buena forma de saber como funcionan las cosas.
Una introducción mas exhaustiva se puede obtener de este link. [como empezar]( http://hello.processing.org/)

Finalmente, para finalizar con nuestro ejercicio de introducción: realizaremos un pequeño y simple programa para que el circulo siga la posición de cursor.

```
void setup()
{
  size(250, 250);
  background(0);
}

void draw()
{
  fill(255, 0, 0);
  ellipse(mouseX, mouseY, 60, 60);  
 //  mouseX y mouseY se usan para obtener las coordenadas de la posición del cursor
}
```
Ahora podemos dotar un programa de __Processing__ de algún tipo de inteligencia para que el pueda dibujar por si solo, nosotros solo nos preocuparemos de darle al programa las herramientas y parámetros de cómo dibujar.

Empezamos por decir que las cosas construidas por el hombre siempre son edificaciones no muy complejas, lo mas importante es que sean solidas y resistentes con el pasar del tiempo. Esto en detalles técnicos, pero un punto importante y un tanto espiritual es que las construcciones del hombre siempre se fijan de afuera hacia adentro. En una casa por ejemplo; se empieza por fijar las bases, luego columnas, después los muros y así sucesivamente, siguiendo rigurosamente unos estándares para que esta edificación no decaiga.

Particularmente, la evolución humana y de la naturaleza no funciona de este modo. Se trata de un proceso de errores de millones de años, donde no se crea y se construye como hace el hombre, sino que se evoluciona y se complejizan esos métodos de nacimiento. Hablo entonces aquí de adentro hacia fuera, y es de este modo como funciona la naturaleza.

En el mundo del arte, los programas que toman una decisión sobre como evolucionar siguiendo unos parámetros son llamados generativos. Pero estos parámetros son decididos por el programa al azar, no son impuestos como cuando queremos que se dibuje un circulo con tal color en tal posición.

Empecemos por preguntarnos entonces, ¿ Que es lo que hace la función ellipse de __Processing__ ? la respuesta es simple, “pues dibujar un circulo”, eso es verdad, pero si desintegramos ese método que trae por defecto el programa, nos daremos cuenta que esconde unos métodos matemáticos para realizar el dibujo del circulo.

Miremos el siguiente código:

```
// creación de circulo con funciones trigonométricas

//radio del circulo
float radius = 45;  

//Angulo de inicio
int angle = 180;

//Coordenadas centrales del circulo
int centerx = 250;
int centery = 150;

void setup()
 {
  size(500, 300);
  background(255);
  strokeWeight(5);

  //creacion del circulo con la función de processing
  stroke(0, 30);
  noFill();
  ellipse(centerx, centery, radius*2, radius*2);
}

void draw()
 {

  angle ++;
  float rad = radians(angle);

  float x = centerx + (radius * cos(rad)); 
  float y = centery + (radius * sin(rad)); 
  stroke(20, 50, 70);
  point(x, y);
}
```

En el anterior código realizamos uso de funciones trigonométricas como “sin()” y “cos()”, las cuales son métodos matemáticos para dibujar funciones periódicas.
De esta manera, nos entrañamos en la función ellipse() y realizamos nuestro propio método de dibujado de un circulo.

Ahora tomando este punto de partida, y añadiendo algo de “random();”, podemos dictarle al programa que cree una figura dentro de un estándar que es el método de dibujar un circulo que discernimos anteriormente.

```
//Varianza sobre seno 

float radius = 100; 
int centX = 250; 
int centY = 150;

void setup () 
{ 
  size(500, 300); 
  background(255); 
  strokeWeight(5); 
  smooth();

  stroke(0, 30);
  noFill(); 
  ellipse(centX, centY, radius*2, radius*2);
  stroke(20, 50, 70);
  strokeWeight(1);

  float x, y;
  float noiseval = random(10);

  //valores de random en el start point
  float radVariance, thisRadius, rad;
  beginShape();
  fill(20, 50, 70, 50);
  for (float ang=0;ang<=360;ang+=1) {

    //valores de random en el start point

    noiseval += 0.1;
    radVariance = 30 * customNoise(noiseval);


    thisRadius = radius + radVariance; 
    rad = radians(ang);
    x = centX + (thisRadius * cos(rad)); 
    y = centY + (thisRadius * sin(rad));
    curveVertex(x, y);
  }
  endShape();
}
float customNoise(float value) { 
  float retValue = pow(sin(value), 3); 
  return retValue;
}
```

Siempre que ejecutamos el código, por decirlo de alguna manera; los pétalos no nacen en el mismo lugar, siempre giran en sentido horario de las agujas del reloj, permitiendo que siempre sea diferente de la anterior.

Por ultimo observaremos otro ejemplo para observar la evolución de una imagen en __processing__:

```
//  Johnny Sepúlveda
//  2013

float radius = 100; 
int centX = 250; 
int centY = 250;
float lastx_1 = -999;
float lasty_1 = -999;
float lastx_2 = 999;
float lasty_2 = 999;
float x1, y1, x2, y2;
float radio_noise = random(10);
float angulo;

void setup()
{
  size(500, 500); 
  background(255); 
  strokeWeight(5); 
  smooth();
  frameRate(29);
  //stroke(0, 30);
  //noFill(); 
  //ellipse(centX, centY, radius*2, radius*2);
}

void draw()
{
  angulo++; 
  radio_noise += 0.05;

  if ( angulo > 360)
    angulo = -360;

  float rad_1 = radians(angulo);
  float rad_2 = rad_1 + PI ;
  float mi_radio_1 = noise((radio_noise) * rad_2) * random(width-500); 
  float mi_radio_2 = noise((radio_noise) * rad_2) * random(height-500); 
  x1 = centX +(mi_radio_1  +  miRuido_1(random(100))  * cos( rad_1) ) ;
  y1 = centY +(mi_radio_2  +  miRuido_2(random(100))  * sin( rad_1) );
  x2 = centX + (noise(mi_radio_1)*random(250)  * cos(rad_2));
  y2 = centY + (noise(mi_radio_2)*random(250)  * sin(rad_2));

  if ((lastx_1 > -999) && (lastx_2 < 999))
  {

    strokeWeight(0.5);
    stroke(random(20), random(50), random(15), random(150));
    fill (255, 0, 0, 20);
    line(x1, y1, x2, y2);
  }

  lastx_1 = x1;
  lasty_1 = y1;
  lastx_2 = x2;
  lasty_2 = y2;
}

float miRuido_1(float value)
{
  value = random(10);
  float counter = int( value % width);
  float retValue = pow(sin(value), counter);
  float rev = retValue + noise(value)-100;
  return rev;
}

float miRuido_2(float value)
{
  value = random(10);
  float counter = int( value % height);
  float retValue = pow(cos(value), counter);
  float rev = retValue + noise(value)-100;
  return rev;
}
```

Como observamos, __Processing__  puede ser una excelente herramienta de creación visual, puede hacernos nuestra imaginación volar porque podemos crear desde cero cualquier imagen, inclusive podemos dotarlo de cierta inteligencia para que el solito pueda tomar sus propias decisiones y  elegir al azar que método escoge para evolucionar.

Para terminar podemos decir que Processing es de libre descarga, eso quiere decir que puedes instalarlo en tu computadora de manera totalmente legal, ya que se trata de software libre. Es tu voluntad hacer aportes voluntarios de manera económica al equipo desarrollador del software, ya que se trata de que personas como tú o como yo nos acerquemos mas a las herramientas que nos prevén las computadoras para adquirir un conocimiento y hacer esas maquinas útiles al servicio de nosotros, y eliminar esa barrera que el software privativo promueve con sus códigos cerrados.

